#!/usr/local/bin/gnuplot -p

#
# plots dispersion curves
# @author Tobias Weber <tweber@ill.fr>
# @date jan-2022
# @license GPLv3, see 'LICENSE' file
#

# 1: pdf, 2: png, others: screen
out_term = 1

if(out_term == 1) {
	set term pdf color enhanced font "Helvetica, 50" size 20, 15
}
if(out_term == 2) {
	set term pngcairo enhanced font "Helvetica, 28" size 1333, 1000
}


# q range to plot
qrange  = 0.5
# E range to plot
Erange  = 15.0

show_legend = 0
point_size = 1.0


# input data file
file_dyn = "disp.dat"

# output files
if(out_term == 1) {
	file_out = "disp.pdf"
	set output file_out
}
if(out_term == 2) {
	file_out = "disp.png"
	set output file_out
}


# colours
col_axis  = "#000000"  # colour for coordinate cross
col_full  = "#ff0000"  # colour for all weights


# ranges
set xrange  [ -qrange : qrange ]
set yrange  [ -Erange : Erange ]


# ticks
set xtics 0.2 nomirror
set mxtics 2

set ytics 2 nomirror
set mytics 2


# labels
set xlabel "{/Helvetica-Italic q}_{h} (rlu)"
set ylabel "{/Helvetica-Italic E} (meV)"


# legend
if(show_legend != 0) {
        set key at graph 0.49, graph 0.97 height 0.3 samplen 0 Left reverse box opaque
}
else {
        unset key
}


# show coordinate cross
set arrow 1 from 0,-Erange to 0,Erange lw 2 dt 2 lc rgb col_axis nohead
set arrow 2 from -qrange,0 to qrange,0 lw 2 dt 2 lc rgb col_axis nohead


plot \
	file_dyn using ($3):($4):($5*point_size) \
		with points pt 7 ps var lc rgb col_full title "Dispersion"
